import java.nio.file.FileAlreadyExistsException
import java.nio.file.Files
import java.nio.file.Paths
import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: "com.github.johnrengelman.shadow"

dependencies {
    shadow project(":nuvotifier-bukkit")
    shadow project(":nuvotifier-bungeecord")
    shadow project(":nuvotifier-sponge")
    shadow project(":nuvotifier-velocity")
    shadow 'ninja.leaping.configurate:configurate-core:3.3'
    shadow 'ninja.leaping.configurate:configurate-yaml:3.3'
}

shadowJar {
    configurations = [project.configurations.shadow]

    baseName = "nuvotifier"
    classifier = null
    
    relocate "io.netty", "com.vexsoftware.votifier.netty"
    relocate "org.json", "com.vexsoftware.votifier.json"
    relocate "com.google.code", "com.vexsoftware.votifier.google.code"
    relocate "ninja.leaping", "com.vexsoftware.votifier.ninja.leaping"
    relocate "com.google.common", "com.vexsoftware.votifier.google.common"
    relocate "com.google.guava", "com.vexsoftware.votifier.google.guava"
    relocate "org.apache.commons.io", "com.vexsoftware.votifier.commons.io"
}

artifacts {
    tasks.shadowJar.outputs.files.each {
        archives(it) {
            builtBy shadowJar
        }
    }
}

def artifactPattern = Pattern.compile(/(.*)-([0-9]+\.)*[0-9]+(-SNAPSHOT)?\.jar/)

task("linkLatest") {
    dependsOn tasks.shadowJar
    doLast {
        tasks.shadowJar.outputs.files.each {
            System.println(it.absolutePath)
            def noVerMatcher = artifactPattern.matcher(it.absolutePath)
            if (!noVerMatcher.matches())
                throw new IllegalStateException("wtf")

            def noVer = Paths.get(noVerMatcher.group(1) + ".jar")
            def versnd = it.toPath()
            try {
                Files.createSymbolicLink(noVer, versnd)
            } catch (FileAlreadyExistsException ignored) {
            }
        }
    }
}

build.dependsOn linkLatest
